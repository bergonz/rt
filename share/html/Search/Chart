%# BEGIN BPS TAGGED BLOCK {{{
%#
%# COPYRIGHT:
%#
%# This software is Copyright (c) 1996-2013 Best Practical Solutions, LLC
%#                                          <sales@bestpractical.com>
%#
%# (Except where explicitly superseded by other copyright notices)
%#
%#
%# LICENSE:
%#
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%#
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%#
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%#
%#
%# CONTRIBUTION SUBMISSION POLICY:
%#
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%#
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%#
%# END BPS TAGGED BLOCK }}}
<%args>
$Cache => undef
$Query => "id > 0"
@GroupBy => 'Queue'
$ChartStyle => 'bars'
@ChartFunction => 'COUNT'
</%args>
<%init>
use GD;
use GD::Text;

@ChartFunction = grep defined && length, @ChartFunction;
$ChartStyle = 'bars' if @ChartFunction > 1;

my $chart_class;
if ($ChartStyle eq 'pie') {
    require GD::Graph::pie;
    $chart_class = "GD::Graph::pie";
} else {
    require GD::Graph::bars;
    $chart_class = "GD::Graph::bars";
}

use RT::Report::Tickets;
my $report = RT::Report::Tickets->new( $session{'CurrentUser'} );

my %columns;

if ( $Cache and my $data = delete $session{'charts_cache'}{ $Cache } ) {
    %columns = %{ $data->{'columns'} };
    $report->Deserialize( $data->{'report'} );
    $session{'i'}++;
} else {
    foreach my $e ( splice @GroupBy ) {
        next unless defined $e && length $e;
        next unless $report->IsValidGrouping( Query => $Query, GroupBy => $e );
        push @GroupBy, $e;
    }

    %columns = $report->SetupGroupings(
        Query => $Query,
        GroupBy => \@GroupBy,
        Function => \@ChartFunction,
    );

    $report->SortEntries;
}

my @data = ([],[]);
my $max_value = 0;
my $max_key_length = 0;
while ( my $entry = $report->Next ) {
    my $key = join ' - ', map $entry->LabelValue( $_ ), @{ $columns{'Groups'} };

    my @values = map $entry->RawValue($_), @{ $columns{'Functions'} };
    if ($chart_class eq 'GD::Graph::pie') {
        $key .= ' - '. $entry->LabelValue( $columns{'Functions'}[0] );
    }

    push @{ $data[0] }, $key;

    my $i = 0;
    push @{ $data[++$i] }, $_ foreach @values;

    foreach my $v ( @values ) {
        $max_value = $v if $max_value < $v;
    }
    $max_key_length = length $key if $max_key_length < length $key;
}

my %font_config = RT->Config->Get('ChartFont');
my $font = $font_config{ $session{CurrentUser}->UserObj->Lang || '' }
    || $font_config{'others'};

# Pie charts don't like having no input, so we show a special image
# that indicates an error message. Because this is used in an <img>
# context, it can't be a simple error message. Without this check,
# the chart will just be a non-loading image.
unless ( $report->Count ) {
    my $plot = GD::Image->new(600 => 400);
    $plot->colorAllocate(255, 255, 255); # background
    my $black = $plot->colorAllocate(0, 0, 0);

    require GD::Text::Wrap;
    my $error = GD::Text::Wrap->new($plot,
        color => $black,
        text  => loc("No tickets found."),
    );
    $error->set_font( $font, 16 );
    $error->draw(0, 0);

    $m->comp( 'SELF:Plot', plot => $plot, %ARGS );
}

my $chart = $chart_class->new( 600 => 400 );
$chart->set( pie_height => 60 ) if $chart_class eq 'GD::Graph::pie';
$chart->set_title_font( $font, 16 ) if $chart->can('set_title_font');
$chart->set_legend_font( $font, 16 ) if $chart->can('set_legend_font');
$chart->set_x_label_font( $font, 14 ) if $chart->can('set_x_label_font');
$chart->set_y_label_font( $font, 14 ) if $chart->can('set_y_label_font');
$chart->set_label_font( $font, 14 ) if $chart->can('set_label_font');
$chart->set_x_axis_font( $font, 12 ) if $chart->can('set_x_axis_font');
$chart->set_y_axis_font( $font, 12 ) if $chart->can('set_y_axis_font');
$chart->set_values_font( $font, 12 ) if $chart->can('set_values_font');
$chart->set_value_font( $font, 12 ) if $chart->can('set_value_font');

if ($chart_class eq "GD::Graph::bars") {
    my %args;
    my $count = @{ $data[0] };
    $args{'bar_spacing'} =
        $count > 30 ? 1
        : $count > 20 ? 2
        : $count > 10 ? 3
        : 5
    ;
    if ( my $code = $report->First->LabelCode( $columns{'Functions'}[0] ) ) {
        my %info = %{ $report->ColumnInfo( $columns{'Functions'}[0] ) };
        $args{'values_format'} = $args{'y_number_format'} = sub {
            return $code->($report, %info, VALUE => shift );
        };
    }
    $report->GotoFirstItem;

    $chart->set(
        %args,
        x_label => join( ' - ', map $report->Label( $_ ), @{ $columns{'Groups'} } ),
        x_label_position => 0.6,
        y_label => $report->Label( $columns{'Functions'}[0] ),
        y_label_position => 0.6,
        show_values => 1,
        values_space => -1,
# use a top margin enough to display values over the top line if needed
        t_margin => 18,
# the following line to make sure there's enough space for values to show
        y_max_value => 5*(int($max_value/5) + 2),
# if there're too many bars or at least one key is too long, use vertical
        x_labels_vertical => ( $count * $max_key_length > 60 ) ? 1 : 0,
        bargroup_spacing => $args{'bar_spacing'}*5,
    );
}

# refine values' colors, with both Color::Scheme's help and my own tweak
$chart->{dclrs} = [
    '66cc66', 'ff6666', 'ffcc66', '663399',
    '3333cc', '339933', '993333', '996633',
    '33cc33', 'cc3333', 'cc9933', '6633cc',
];

{
    no warnings 'redefine';
    *GD::Graph::pick_data_clr = sub {
        my $self      = shift;
        my $color_hex = $self->{dclrs}[ $_[0] % @{ $self->{dclrs} } - 1 ];
        return map { hex } ( $color_hex =~ /(..)(..)(..)/ );
    };
}

my $plot = $chart->plot( \@data ) or die $chart->error;
$m->comp( 'SELF:Plot', plot => $plot, %ARGS );
</%init>

<%METHOD Plot>
<%ARGS>
$plot => undef
</%ARGS>
<%INIT>
my @types = ('png', 'gif');
for my $type (@types) {
    $plot->can($type)
        or next;

    $r->content_type("image/$type");
    $m->out( $plot->$type );
    $m->abort();
}

die "Your GD library appears to support none of the following image types: " . join(', ', @types);
</%INIT>

</%METHOD>
